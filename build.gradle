apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'c'
apply plugin: 'distribution'

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

model {
    components {
        curses(NativeLibrarySpec) {
            targetPlatform "mac_64"
            targetPlatform "linux_32"
            targetPlatform "linux_64"
            targetPlatform "windows_64"
            targetPlatform "windows_32"

            binaries {
                all {
		 if (targetPlatform.name == 'mac_64') { 
                    cCompiler.args "-I/System/Library/Frameworks/JavaVM.framework/Versions/A/Headers", "-Xdebug"
	          }
		  if (targetPlatform.name == 'linux_32' || targetPlatform.name == 'linux_64') {
                    cCompiler.args "-I$System.env.JAVA_HOME/include", "-I$System.env.JAVA_HOME/include/linux" 
		  }
                  linker.args  "-lncurses"
                }
            }
        }
    }

    platforms {
        windows_32 {
            architecture 'i386'
            operatingSystem 'windows'
        }

        windows_64 {
            architecture 'x64'
            operatingSystem 'windows'
        }

        mac_64 {
            architecture 'x86_64'
            operatingSystem 'mac os x'
        }

        linux_64 {
            architecture 'x86_64'
            operatingSystem 'linux'
        }

        linux_32 {
            architecture 'i386'
            operatingSystem 'linux'
        }
    }
}


dependencies {
    testCompile 'junit:junit-dep:4.11'
    testCompile('org.mockito:mockito-core:1.9.5') {
        transitive = false
    }
    testCompile 'org.hamcrest:hamcrest-core:1.3'
}

distributions {
    main {
        baseName = 'jtext'
        contents {

            from(configurations.compile) {
                into 'lib'
            }

            from("$libsDir/curses/shared") {
                into 'native'
            }

            from(libsDir) {
                include '*.jar'
                into 'lib'
            }

        }
    }
}

distZip.dependsOn(jar)
distTar.dependsOn(jar)


